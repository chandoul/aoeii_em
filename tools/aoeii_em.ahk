#Requires AutoHotkey v2
#SingleInstance Force

#Include ..\Libs\Base.ahk
#Include ..\libs\JSON.ahk
#Include ..\libs\webview2\WebViewToo.ahk

aoeiiapp := Base()
aoeiiapp.__Startup()
gameapp := Game()

toolItems := []
For key, tool in aoeiiapp.tools {
    if key != '00_ungame'
        toolItems.Push(Map('key', key, 'title', tool['title']))
}

webViewDataDir := EnvGet('LOCALAPPDATA') '\aoeii_em\WebView2'
webViewLoader := aoeiiapp.workDirectory '\libs\webview2\' (A_PtrSize * 8) 'bit\WebView2Loader.dll'
if !FileExist(webViewLoader)
    throw Error('The WebView2 loader is missing: ' webViewLoader)
DirCreate(webViewDataDir)
aoeiiGui := WebViewGui('-Caption +Resize', aoeiiapp.name, , {
    DefaultWidth: 820,
    DefaultHeight: 490,
    DataDir: webViewDataDir,
    DllPath: webViewLoader
})
aoeiiGui.OnEvent('Close', (*) => ExitApp())
aoeiiGui.Control.wv.add_WebMessageReceived(handleWebMessage)
aoeiiGui.Control.BrowseFolder(aoeiiapp.workDirectory, 'aoeii.localhost')
aoeiiGui.Control.Navigate('https://aoeii.localhost/webview2/index.html')
aoeiiGui.Show()

aoeiiapp.isGameFolderSelected()

sendUiMessage(message) {
    aoeiiGui.Control.wv.PostWebMessageAsJson(JSON.Dump(message))
}

handleWebMessage(sender, args) {
    request := JSON.Load(args.WebMessageAsJson)
    if !(request is Map) || !request.Has('action')
        throw Error('Invalid message received from the WebView UI.')

    switch request['action'] {
        case 'ready':
            sendUiMessage(Map(
                'type', 'state',
                'name', aoeiiapp.name,
                'description', aoeiiapp.description,
                'version', aoeiiapp.version,
                'license', aoeiiapp.license,
                'gameLocation', aoeiiapp.gameLocation,
                'dpiWarning', A_ScreenDPI > 96,
                'tools', toolItems,
                'games', Map(
                    'aok', !!FileExist(aoeiiapp.gameLocation '\empires2.exe'),
                    'aoc', !!FileExist(aoeiiapp.gameLocation '\age2_x1\age2_x1.exe'),
                    'hd', !!FileExist(aoeiiapp.gameLocation '\age2_x1\age2_x2.exe')
                )
            ))
        case 'about':
            MsgBoxEx(
                'A homemade tool humbly made by Smile, enjoy!'
                . '`n> Description: ' aoeiiapp.description
                . '`n> Scripting Language: AutoHotkey'
                . '`n> Name: ' aoeiiapp.name
                . '`n> Version: ' aoeiiapp.version
                . '`n> License: ' aoeiiapp.license
                , aoeiiapp.name, , 0x40
            )
        case 'open-game-folder':
            Run(aoeiiapp.gameLocation)
        case 'reload':
            Reload()
        case 'close':
            ExitApp()
        case 'repair':
            performGameAnalyze()
        case 'update':
            updateCheck()
        case 'launch-tool':
            key := request['key']
            if !aoeiiapp.tools.Has(key) || key = '00_ungame'
                throw Error('Unknown tool requested by the WebView UI: ' key)
            tool := aoeiiapp.tools[key]
            Run(Format('{}', tool['run']), tool['workdir'])
        case 'launch-game':
            switch request['key'] {
                case 'aok':
                    path := aoeiiapp.gameLocation '\empires2.exe'
                case 'aoc':
                    path := aoeiiapp.gameLocation '\age2_x1\age2_x1.exe'
                case 'hd':
                    path := aoeiiapp.gameLocation '\age2_x1\age2_x2.exe'
                default:
                    throw Error('Unknown game requested by the WebView UI: ' request['key'])
            }
            if FileExist(path)
                Run(path, aoeiiapp.gameLocation)
        default:
            throw Error('Unsupported action received from the WebView UI: ' request['action'])
    }
}

; Update check
updateCheck(*) {
    sendUiMessage(Map('type', 'update-status', 'checking', true))
    aoeiiapp.appUpdateCheck()
    sendUiMessage(Map('type', 'update-status', 'checking', false))
}

performGameAnalyze(*) {
    static infoGui := 0
    choice := MsgBoxEx(
        Format(
            'Check list:`n`n{}`n{}`n{}`n{}`n{}`n{}`n`n{}',
            '1 - Delayed start (Windows Vista/7)',
            '2 - Corrupted file',
            '3 - The Conquerors Application location',
            '4 - The Game Save Folder',
            '5 - The Game Update',
            '6 - The Game Corrupted Files',
            'The app will try to fix these issues, do you wish to continue?'
        ), 'Check list', 4, 0x40
    ).result

    If choice != 'Yes'
        return

    If !infoGui {
        infoGui := GuiEx(, 'Package Download')
        infoGui.initiate(0, , 0)
        infoGui.OnEvent('Close', (*) => Reload())
        infoText := infoGui.AddEdit('-E0x200 Border ReadOnly xm ym+20 w350 Center -VScroll BackgroundFFAD59', '...')
        InfoBar := infoGui.AddProgress('-smooth wp h18 Range1-6')
    }

    infoGui.ShowEx(, 1)

    infoText.Text := 'Gameux Win7/Vista fix...'
    ; Gameux Win7/Vista auto fix
    GEs := [
        A_WinDir '\System32\gameux.dll',
        A_WinDir '\SysWOW64\gameux.dll'
    ]
    For GE in GEs {
        Switch SubStr(A_OSVersion, 1, 3) {
            Case '6.0', '6.1':
                If FileExist(GE) {
                    RunWait(Format(A_ComSpec ' /c takeown /f {}', GE), , 'Hide')
                    RunWait(Format(A_ComSpec ' /c cacls {} /E /P %username%:F', GE), , 'Hide')
                    RunWait(Format(A_ComSpec ' /c ren {} gameux_renamed.dll', GE), , 'Hide')
                }
        }
    }
    InfoBar.Value += 1

    infoText.Text := 'Checking for corrupted file...'
    ; Check for a corrupted file
    md5 := '7c1ae22e8f9d385d51b4f2eadd2a6d76'
    dlltargets := [aoeiiapp.gameLocation '\dsound.dll', aoeiiapp.gameLocation '\age2_x1\dsound.dll']
    For target in dlltargets {
        if FileExist(target) && md5 = aoeiiapp.hashFile(, target) {
            FileDelete(target)
        }
    }
    InfoBar.Value += 1

    infoText.Text := 'Checking for missing files...'
    ; Fix aoc wrong exe location
    aocexe := aoeiiapp.gameLocation '\age2_x1.exe'
    If FileExist(aocexe) {
        if !DirExist(aoeiiapp.gameLocation '\Age2_x1')
            DirCreate(aoeiiapp.gameLocation '\Age2_x1')
        FileMove(aocexe, aoeiiapp.gameLocation '\Age2_x1\', 1)
    }
    InfoBar.Value += 1

    infoText.Text := 'Creating Multi folder in SaveGame if not exist...'
    ; Create Multi folder in SaveGame if not exist
    If !DirExist(aoeiiapp.gameLocation '\SaveGame\Multi') {
        DirCreate(aoeiiapp.gameLocation '\SaveGame\Multi')
    }
    InfoBar.Value += 1

    infoText.Text := 'Checking for existing fixes...'
    ; Check if no fix exists
    fix := ''
    ignoreFiles := Map('wndmode.dll', 1, 'windmode.dll', 1)
    Loop Files, aoeiiapp.workDirectory '\tools\fix\*', 'D' {
        if aoeiiapp.folderMatch(A_LoopFileFullPath, aoeiiapp.gameLocation, ignoreFiles) {
            fix := A_LoopFileName
        }
    }
    If fix = '' {
        RunWait(aoeiiapp.tools['02_fix']['run'] ' "Update v05"')
        aoeiiapp.applyDDrawFix()
    }
    InfoBar.Value += 1

    infoText.Text := 'Checking for missing files...'
    ; Check for missing files
    gameLink := 'https://github.com/chandoul/aoeii_em/raw/refs/heads/master/packages/Age%20of%20Empires%20II.7z'
    files := JSON.LoadFile('gamefiles.json')

    For file in files {
        if !FileExist(aoeiiapp.gameLocation '\' file['path']) {
            If !aoeiiapp.downloadPackage(gameLink, gameapp.gamePackage)
                Return
            RunWait(Format('"{}" x "{}" "{}" -o"{}"', aoeiiapp._7zrCsle, gameapp.gamePackage, file['path'], aoeiiapp.gameLocation))
        }
    }
    InfoBar.Value += 1
    infoGui.Hide()

    MsgBoxEx(
        'Verification is done, you should be able to play your game normally by now!'
        , aoeiiapp.name, , 0x40
    )
}

; Multiline chat send
; GameRanger
GroupAdd('GRChat', 'Room ahk_exe GameRanger.exe')
GroupAdd('GRChat', 'Message ahk_exe GameRanger.exe')
; Age of Empires
GroupAdd('AOEII', 'ahk_exe empires2.exe')
GroupAdd('AOEII', 'ahk_exe age2_x1.exe')

chatSpam := aoeiiapp.readConfiguration('chatSpam')

#HotIf (WinActive('ahk_group GRChat') || WinActive('ahk_group AOEII')) && chatSpam
^!v:: {
    For line in StrSplit(A_Clipboard, '`r`n') {
        SendInput('{Raw}' line)
        SendInput('{Enter}')
        Sleep(10)
    }
}
^!b:: {
    text := InputBox('Text to send', , 'h100').Value
    times := InputBox('Number of times to send', , 'h100').Value
    A_Clipboard := ''
    Loop times {
        A_Clipboard .= text '`n'
    }
}
#HotIf